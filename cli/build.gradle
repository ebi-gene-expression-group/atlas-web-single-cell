plugins {
    id 'atlas-web-app.java-conventions'
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

version = '1.0.0'

bootJar {
    archiveFileName = 'atlas-cli-sc.jar'
}

dependencies {
    implementation project(':atlas-web-core')
    implementation project(':app')

    runtimeOnly 'javax.servlet.jsp:jsp-api:2.2'

    implementation 'info.picocli:picocli:4.6.1'
    implementation 'info.picocli:picocli-spring-boot-starter:4.6.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

processResources {
    filesMatching('configuration.properties') {
        expand(
                dataFilesLocation: project.findProperty('dataFilesLocation') ?: "${System.env.ATLAS_DATA_PATH}",
                experimentFilesLocation: project.findProperty('experimentFilesLocation') ?: "${System.env.ATLAS_DATA_PATH}/scxa",
                experimentDesignLocation: project.findProperty('experimentDesignLocation') ?: "${System.env.ATLAS_DATA_PATH}/scxa/expdesign"
        )
    }

    filesMatching('jdbc.properties') {
        expand(
                jdbcUrl: project.findProperty('jdbcUrl') ?: "jdbc:postgresql://${System.env.ATLAS_POSTGRES_HOST}:5432/${System.env.ATLAS_POSTGRES_DB}",
                jdbcUsername: project.findProperty('jdbcUsername') ?: "${System.env.ATLAS_POSTGRES_USER}",
                jdbcPassword: project.findProperty('jdbcPassword') ?: "${System.env.ATLAS_POSTGRES_PASSWORD}"
        )
    }

    filesMatching('solr.properties') {
        expand(
                zkHosts: project.findProperty('zkHosts') ?: "${System.env.ATLAS_ZK_HOSTS}",
                solrHosts: project.findProperty('solrHosts') ?: "${System.env.ATLAS_SOLR_HOSTS}",
                solrUser: project.findProperty('solrUser') ?: "${System.env.ATLAS_SOLR_USER}",
                solrPassword: project.findProperty('solrPassword') ?: "${System.env.ATLAS_SOLR_PASSWORD}"
        )
    }
}
