plugins {
	id 'atlas-web-app.java-conventions'
	id 'org.springframework.boot' version '2.5.6'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.github.johnrengelman.processes' version '0.5.0'
	id 'org.springdoc.openapi-gradle-plugin' version '1.3.3'
}

group = 'uk.ac.ebi.ge'
sourceCompatibility = '11'

dependencies {
	implementation project(':atlas-web-core')
	implementation project(':app')

	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'javax.servlet.jsp:jsp-api:2.2'

	implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.5.12'
	runtimeOnly 'org.springdoc:springdoc-openapi-ui:1.5.12'
}

openApi {
	apiDocsUrl = 'http://localhost:8080/v3/api-docs.yaml'
	outputFileName = 'openapi.yaml'
}

processResources {
	filesMatching('configuration.properties') {
		expand(
				dataFilesLocation:
						project.hasProperty('dataFilesLocation') ?
								dataFilesLocation :
								"${System.env.ATLAS_DATA_PATH}",
				experimentFilesLocation:
						project.hasProperty('experimentFilesLocation') ?
								experimentFilesLocation :
								"${System.env.ATLAS_EXPERIMENTS_PATH}",
		)
	}

	filesMatching('jdbc.properties') {
		expand(
				jdbcUrl:
						project.hasProperty('jdbcUrl') ?
								jdbcUrl :
								"jdbc:postgresql://${System.env.ATLAS_POSTGRES_HOST}:5432/${System.env.ATLAS_POSTGRES_DB}",
				jdbcUsername:
						project.hasProperty('jdbcUsername') ?
								jdbcUsername :
								"${System.env.ATLAS_POSTGRES_USER}",
				jdbcPassword:
						project.hasProperty('jdbcPassword') ?
								jdbcPassword :
								"${System.env.ATLAS_POSTGRES_PASSWORD}"
		)
	}

	filesMatching('solr.properties') {
		expand(
				zkHost:
						project.hasProperty('zkHost') ?
								zkHost :
								"${System.env.ATLAS_ZK_HOST}",
				zkPort:
						project.hasProperty('zkPort') ?
								zkPort :
								'2181',
				solrHost:
						project.hasProperty('solrHost') ?
								solrHost :
								"${System.env.ATLAS_SOLR_HOST}",
				solrPort:
						project.hasProperty('solrPort') ?
								solrPort :
								'8983'
		)
	}
}
